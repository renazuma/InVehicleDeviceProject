apply plugin: 'android'

android {
    compileSdkVersion getAndroidCompileSdkVersion()
    buildToolsVersion getAndroidBuildToolsVersion()
    defaultConfig {
        versionCode computeVersionCode()
        versionName computeVersionName()
        testInstrumentationRunner "com.kogasoftware.odt.invehicledevice.testutil.DexMakerInstrumentationTestRunner"
    }
    signingConfigs {
        release {
            storeFile file(System.properties["ksc.android.sign.keystore"] ?: ".")
            storePassword System.properties["ksc.android.sign.storepass"]
            keyAlias System.properties["ksc.android.sign.alias"]
            keyPassword System.properties["ksc.android.sign.keypass"]
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        [  // TODO: Write these on library project
            'META-INF/LICENSE',
            'META-INF/LICENSE.txt',
            'META-INF/NOTICE',
            'META-INF/NOTICE.txt',
        ].each { exclude it }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'src/main/libs', include: '*.jar')
    androidTestCompile fileTree(dir: 'src/androidTest/libs', include: '*.jar')
    compile project(':AndroidOpenJTalk')
    compile project(':InVehicleDeviceApiClient')
    compile project(':play_apk_expansion')
}
