apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

// リリース用APKを作る際に、バージョンコードをインクリメントするコミットを積むこと
def VERSION_CODE = 20022

android {
    compileSdkVersion 33
    defaultConfig {
        versionCode VERSION_CODE
        versionName computeVersionName(VERSION_CODE)
        testInstrumentationRunner "com.kogasoftware.android.DexMakerInstrumentationTestRunner"
        minSdkVersion 24
        multiDexEnabled true
        targetSdkVersion 31
    }
    signingConfigs {
        release {
            storeFile file(System.properties["ksc.android.sign.keystore"] ?: ".")
            storePassword System.properties["ksc.android.sign.storepass"]
            keyAlias System.properties["ksc.android.sign.alias"]
            keyPassword System.properties["ksc.android.sign.keypass"]
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt']
        }
    }
    useLibrary 'org.apache.http.legacy'
    useLibrary 'android.test.base'
    useLibrary 'android.test.runner'
    lint {
        abortOnError false
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(include: '*.jar', dir: 'src/main/libs')
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    androidTestImplementation fileTree(include: '*.jar', dir: 'src/androidTest/libs')
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation "com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava"
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.0"
    implementation 'com.squareup.moshi:moshi-kotlin:1.14.0'
}

def computeVersionName(int versionCode) {
    def versionCodeVal = versionCode.toString()
    // Use SimpleDateFormat to set Locale
    def date = new java.text.SimpleDateFormat('yyyyMMddHHmmss', Locale.US).format(new Date())
    "Build-$versionCodeVal ($date)"
}

project.gradle.taskGraph.whenReady {
    connectedAndroidTest {
    }
}
