buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.1'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'android'

def computeVersionName() {
    def versionCode = computeVersionCode()
    def date = new java.text.SimpleDateFormat('yyyyMMddHHmmss', Locale.US).format(new Date())
    "Build-$versionCode ($date)"
}

def computeVersionCode() {
    (System.getMethod("getenv", String).invoke(null, "BUILD_NUMBER") ?: "0").toInteger()
}

android {
    compileSdkVersion 18
    buildToolsVersion '18.1.1'
    defaultConfig {
        versionCode computeVersionCode()
        versionName computeVersionName()
    }
    signingConfigs {
        release {
            storeFile file(System.properties["ksc.android.sign.keystore"] ?: ".")
            storePassword System.properties["ksc.android.sign.storepass"]
            keyAlias System.properties["ksc.android.sign.alias"]
            keyPassword System.properties["ksc.android.sign.keypass"]
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

def libDir = file('src/main/libs')

dependencies {
    compile fileTree(dir: libDir, include: '*.jar')
    compile('com.google.android.gms:play-services:3.2.65') {
        // eclipse用のlibsの中にあるsupport-v4と混ざり、"IAE: already added"が発生するため除外
        exclude group: 'com.android.support', module: 'support-v4'
    }
    instrumentTestCompile fileTree(dir: 'src/instrumentTest/libs', include: '*.jar')
    compile project(':AndroidOpenJTalk')
    compile project(':InVehicleDeviceApiClient')
}

/**
 * 普通にビルドすると下記"IAE: already added"が発生するので、dexTestの直前でlibsディレクトリを退避する
 *
 * UNEXPECTED TOP-LEVEL EXCEPTION:
 * java.lang.IllegalArgumentException: already added: Lcom/example/Foo;
 */
gradle.taskGraph.whenReady {taskGraph ->
    def libBackupDir = file("${libDir}.gradle-workaround")
    taskGraph.beforeTask {
        if (it.name == "dexTest") {
            libDir.renameTo(libBackupDir)
        }
    }
    taskGraph.afterTask {
        if (it.name == "packageTest") {
            libBackupDir.renameTo(libDir)
        }
    }
}
